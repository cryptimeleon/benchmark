plugins {
    id 'java-library'
    id 'maven-publish'
}

group = 'de.upb.crypto'
version = '0.0.2' + (Boolean.valueOf(System.getProperty("disablesnapshot")) ? "" : "-SNAPSHOT")

sourceCompatibility = 1.11
targetCompatibility = 1.11
tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

repositories {
    mavenLocal()
    jcenter()
    mavenCentral()
    maven { url "https://nexus.cs.upb.de/repository/sfb901-libs/" }
    maven { url "https://nexus.cs.upb.de/repository/sfb901-releases/" }
    maven { url "https://nexus.cs.upb.de/repository/sfb901-snapshots/" }
}

dependencies {
    api group: 'de.upb.crypto', name: 'craco', version: '1.1.2' + (Boolean.valueOf(System.getProperty("disablesnapshot")) ? "" : "-SNAPSHOT")
    api group: 'de.upb.crypto', name: 'mclwrap', version: '0.0.3' + (Boolean.valueOf(System.getProperty("disablesnapshot")) ? "" : "-SNAPSHOT")

    testCompileOnly group: 'junit', name: 'junit', version: '4.12'
}

test {
    useJUnitPlatform()
    maxParallelForks 4
    //we want display the following test events
    testLogging {
        events "PASSED", "STARTED", "FAILED", "SKIPPED"
    }
    afterSuite { desc, result ->
        if (!desc.parent) { // will match the outermost suite
            def output = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
            def startItem = '|  ', endItem = '  |'
            def repeatLength = startItem.length() + output.length() + endItem.length()
            println('\n' + ('-' * repeatLength) + '\n' + startItem + output + endItem + '\n' + ('-' * repeatLength))
        }
    }
}

task packageTests(type: Jar) {
    archiveClassifier.set("tests")
    from sourceSets.test.output+sourceSets.test.allSource
}

task sourcesJar(type: Jar) {
    archiveClassifier.set("sources")
    from sourceSets.main.allJava
}

task javadocJar(type: Jar) {
    archiveClassifier.set("javadoc")
    from javadoc
}

configurations {
    testArtifacts
}
artifacts {
    archives packageTests
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }

    }

    repositories {
        maven {
            credentials {
                username = System.getProperty('nexus.user')
                password = System.getProperty('nexus.key')
            }
            def releasesRepoUrl = "https://nexus.cs.upb.de/repository/sfb901-releases/"
            def snapshotsRepoUrl = "https://nexus.cs.upb.de/repository/sfb901-snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}

task enableCompositeBuild {
    group = 'Tools'
    description = 'Enable composite build'
    doLast {
        new File("../upb.crypto.craco/.composite-enable").createNewFile()
        new File("../upb.crypto.mclwrap/.composite-enable").createNewFile()
    }
}

task disableCompositeBuild {
    group = 'Tools'
    description = 'Disable composite build'
    doLast {
        File file = file("../upb.crypto.craco/.composite-enable")
        if (file.exists()) {
            file.delete()
        }
        file = file("../upb.crypto.mclwrap/.composite-enable")
        if (file.exists()) {
            file.delete()
        }
    }
}
